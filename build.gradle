plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'fr.sandro642.github'
version = '0.1.0'

task generateVersionClass {
    doLast {
        def versionFile = file("$buildDir/generated/source/version/fr/sandro642/github/update/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.sandro642.github.update;

public class Version {
    public static final String VERSION = "${project.version}";
}
"""
    }
}

compileJava.dependsOn generateVersionClass
sourceSets.main.java.srcDir "$buildDir/generated/source/version"

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

repositories {
    mavenCentral()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spigot API dependency
    compileOnly 'org.spigotmc:spigot-api:1.21.11-R0.2-SNAPSHOT' // The Spigot API with no shadowing. Requires the OSS repo.
}

application {
    mainClass = 'fr.sandro642.github.OreRatio'
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'fr.sandro642.github.OreRatio'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    //withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group.toString()
            artifactId = 'OreRatio'
            version = project.version.toString()
            pom {
                name = 'OreRatio'
                description = ''
                url = 'https://github.com/Sandro642/OreRatio'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://raw.githubusercontent.com/Sandro642/OreRatio/refs/heads/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sandro642'
                        name = 'Sandro'
                        email = 'sandro33810@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/sandro642/OreRatio.git'
                    developerConnection = 'scm:git:ssh://github.com/sandro642/OreRatio.git'
                    url = 'https://github.com/sandro642/OreRatio'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'LocalRepo'
            url = uri(layout.buildDirectory.dir('repo'))
        }
    }
}